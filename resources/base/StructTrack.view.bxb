result-view {
  match {
    StructTrack(this)
  }
  message {
    if (size(this) == 0) {
      template ("Sorry, I found nothing.")
    }
    else-if (size(this)==1) {
      template ("Found Track ID #{value(this.trackID)}")
    }
    else {
      template ("Found #{size(this)} tracks")
    }
  }
  render {
    if (size(this)==1) {
      // detailed view 
      layout {
        section {
          content {
            cell-card {
              slot2 {
                content {
                  order (PrimarySecondary)
                  primary ("#{value(this.trackName)} -- #{value(this.albumTitle)}")
                  secondary("#{value(this.bytes)} bytes -- $#{value(this.unitPrice)}")
                }
              }
            }
          }
        }
      }
    }
    else {
      // summary view 
      list-of (this) {
        where-each (item) {
          layout-macro (MacroList) {
            param (id) {
              expression (item.trackID)
            }
            param (name) {
              expression (item.trackName)
            }
          }
        }
      }
    }
  }
  // end of render 
  conversation-drivers {
    if (size(this)==1) {
      conversation-driver {
        template ("Find album #{value(this.albumID)}")
      }
    }
  }  
}
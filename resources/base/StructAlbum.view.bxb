result-view {
  match {
    StructAlbum(this)
  }
  message {
    if (size(this) == 0) {
      template ("Sorry, I found nothing.")
    }
    else-if (size(this)==1) {
      template ("Found Album ID #{value(this.albumID)}")
    }
    else {
      template ("Found #{size(this)} Albums")
    }
  }
  render {
    if (size(this)==1) {
      // detailed view with each track listed
      layout {
        section {
          content {
            image-card {
              image-object-fit (Contain)
              image-url("#{value(this.imageUrl)}")
              title-area {
                halign (Start)
                slot1 {
                  text {
                    style(Title_M)
                    value ("Title: #{value(this.albumID)} #{value(this.albumTitle)}")
                  }
                }
                slot2 {
                  paragraph {
                    value ("Artist: #{value(this.artistID)} #{value(this.artistName)}")
                    style (Title_S)
                  }
                }
              }
              // on-click
            }
          }
        }
      }
    }
    else {
      list-of (this) {
        where-each (item) {
          layout-macro (MacroList) {
            param (id) {
              expression (item.albumID)
            }
            param (name) {
              expression (item.albumTitle)
            }
          }
        }
      }
    }
  }
  // end of render
  conversation-drivers {
    if (size(this)==1) {
      conversation-driver {
        template ("Find tracks in album #{value(this.albumID)}")
      }
      conversation-driver {
        template ("Find artist #{value(this.artistID)}")
      }
    }
  }
}
